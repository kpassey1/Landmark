     A********************************************************************
     A* Copyright 2012    ** KDP Software Ltd **      All Rights Reserved
     A********************************************************************
     A*
     A*  DISPLAY FILE ID     -    MNU050
     A*  DISPLAY FILE NAME   -       Work with Program Names
     A*  DATE CREATED        -    Jan 11, 2012
     A*
     A******************************************************************
     A                                      PRINT
     A                                      ALTHELP(CA01)
     A                                      CHGINPDFT
     A                                      INDARA
     A                                      HLPTITLE(' ')
     A                                      HELP
     A          R SCR001
     A N79                                  PUTOVR
     A                                      SETOFF(79)
     A  99                                  OVERLAY
     A            USER          10   O  1  2
     A                                      OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A                                      COLOR(BLU)
     A            WKCOMP        30   O  1 26
     A                                      OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
     A                                  1 73DATE EDTCDE(Y)
     A                                      OVRATR
     A                                      DSPATR(HI)
     A                                      COLOR(BLU)
     A            PGM           10   O  2  2
     A                                      OVRATR
     A                                      DSPATR(HI)
     A                                      COLOR(BLU)
     A                                  2 26'   Work with Program Names    '
     A                                      OVRATR
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
     A                                  2 73TIME
     A                                      OVRATR
     A                                      DSPATR(HI)
     A                                      COLOR(BLU)
     A                                  4  2'Type one or more action codes. The-
     A                                      n Enter.                           -
     A                                               '
     A                                      OVRATR
     A                                      COLOR(BLU)
     A                                  5  4'2=Revise'
     A                                      OVRATR
     A                                      COLOR(BLU)
     A                                  5 14'4=Delete'
     A                                      OVRATR
     A                                      COLOR(BLU)
     A                                  5 24'5=Display'
     A                                      OVRATR
     A                                      COLOR(BLU)
     A                                  7  7'Program'
     A                                      OVRATR
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
     A                                  7 19'Secondary'
     A                                      OVRATR
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
     A                                  8  2'Act'
     A                                      OVRATR
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
     A                                  8  7'Name'
     A                                      OVRATR
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
     A                                  8 19'Program'
     A                                      OVRATR
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
     A                                  8 30'Description'
     A                                      OVRATR
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
     A          R SCR001S1
     A                                      SFL
     A  65                                  SFLNXTCHG
     A            FL0001         1  0B  9  3
     A  03                                  DSPATR(UL)
     A                                      COLOR(GRN)
     A                                      EDTCDE(Z)
     A  01                                  DSPATR(PC)
     A  01                                  DSPATR(RI)
     A  04                                  DSPATR(ND)
     A  02                                  DSPATR(PR)
     A            XPPGMN    R     A  O  9  7REFFLD(MPPGMN MNUPGM)
     A                                      DLTEDT
     A                                      COLOR(GRN)
     A            XPSPGM    R     A  O  9 19REFFLD(MPSPGM MNUPGM)
     A                                      DLTEDT
     A                                      COLOR(GRN)
     A  07                                  DSPATR(PC)
     A  07                                  DSPATR(RI)
     A            XPPGMD    R     A  O  9 30REFFLD(MPPGMD MNUPGM)
     A                                      DLTEDT
     A                                      COLOR(GRN)
     A            HRC01          9  0H
     A            HPPGMN    R        H      REFFLD(MPPGMN MNUPGM)
     A                                      ALIAS(ALIAS00001)
     A            HPSPGM    R        H      REFFLD(MPSPGM MNUPGM)
     A                                      ALIAS(ALIAS00002)
     A            INSAV1        99   H
     A          R SCR001C1
     A N79                                  PUTOVR
     A                                      SETOFF(79)
     A                                      SFLCTL(SCR001S1)
     A  64                                  SFLDSP
     A N64                                  ERASE(SCR001S1)
     A  63                                  SFLDSPCTL
     A N63                                  SFLCLR
     A  66                                  SFLINZ
     A N67                                  SFLEND
     A                                      OVERLAY
     A                                      SFLPAG(11)
     A                                      SFLSIZ(11)
     A                                      CF03 INDTXT(03 'Exit')
     A                                      CF06 INDTXT(06 'Create')
     A                                      CF07 INDTXT(07 'Bkwd')
     A                                      CF08 INDTXT(08 'Fwd')
     A                                      CF12 INDTXT(12 'Cancel')
     A                                      ROLLDOWN(62 'Bkwd')
     A                                      ROLLUP(61 'Fwd')
     A                                      HLPCLR
     A          H                           HLPARA(*NONE)
     A                                      HLPPNLGRP(SC0001 -
     A                                      MNU050H)
     A          H                           HLPARA(1 26 1 55)
     A                                      HLPPNLGRP(SC0001WKCOMP -
     A                                      MNU050H)
     A          H                           HLPARA(9 3 19 3)
     A                                      HLPPNLGRP(SC0001FL0001 -
     A                                      MNU050H)
     A          H                           HLPARA(9 7 19 16)
     A                                      HLPPNLGRP(SC0001MPPGMN -
     A                                      MNU050H)
     A          H                           HLPARA(9 19 19 28)
     A                                      HLPPNLGRP(SC0001MPSPGM -
     A                                      MNU050H)
     A          H                           HLPARA(9 30 19 79)
     A                                      HLPPNLGRP(SC0001MPPGMD -
     A                                      MNU050H)
     A          H                           HLPARA(21 18 21 27)
     A                                      HLPPNLGRP(SC0001PS0101 -
     A                                      MNU050H)
     A          H                           HLPARA(21 30 21 39)
     A                                      HLPPNLGRP(SC0001PS0102 -
     A                                      MNU050H)
     A                                      HLPBDY
     A          H                           HLPARA(1 1 24 80)
     A                                      HLPPNLGRP(SC0001 -
     A                                      MNU050H)
     A                                 21  2'Position to . .'
     A                                      OVRATR
     A                                      COLOR(GRN)
     A            PS0101        10   B 21 18
     A                                      OVRDTA
     A                                      OVRATR
     A                                      DSPATR(UL)
     A                                      COLOR(GRN)
     A  05                                  DSPATR(PR)
     A            PS0102        10   B 21 30
     A                                      OVRDTA
     A                                      OVRATR
     A                                      DSPATR(UL)
     A                                      COLOR(GRN)
     A  06                                  DSPATR(PR)
     A                                 23  2'Enter  F1=Help  F3=Exit  F6=Create-
     A                                        F7=Bkwd  F8=Fwd  F12=Cancel  '
     A                                      OVRATR
     A                                      COLOR(BLU)
     A            RRN01          4S 0H      SFLRCDNBR
     A            XRN01          4S 0H
     A          R SCR002
     A N79                                  PUTOVR
     A                                      SETOFF(79)
     A  99                                  OVERLAY
     A                                      CF12 INDTXT(12 'Cancel')
     A                                      CF14 INDTXT(14 'Delete')
     A                                      HLPCLR
     A          H                           HLPARA(*NONE)
     A                                      HLPPNLGRP(SC0002 -
     A                                      MNU050H)
     A          H                           HLPARA(1 26 1 55)
     A                                      HLPPNLGRP(SC0002WKCOMP -
     A                                      MNU050H)
     A          H                           HLPARA(6 29 6 38)
     A                                      HLPPNLGRP(SC0002MPPGMN -
     A                                      MNU050H)
     A          H                           HLPARA(7 29 7 38)
     A                                      HLPPNLGRP(SC0002MPSPGM -
     A                                      MNU050H)
     A          H                           HLPARA(8 29 8 78)
     A                                      HLPPNLGRP(SC0002MPPGMD -
     A                                      MNU050H)
     A          H                           HLPARA(10 29 10 78)
     A                                      HLPPNLGRP(SC0002MPPRM1 -
     A                                      MNU050H)
     A          H                           HLPARA(11 29 11 78)
     A                                      HLPPNLGRP(SC0002MPPRM2 -
     A                                      MNU050H)
     A          H                           HLPARA(12 29 12 78)
     A                                      HLPPNLGRP(SC0002MPPRM3 -
     A                                      MNU050H)
     A          H                           HLPARA(13 29 13 78)
     A                                      HLPPNLGRP(SC0002MPPRM4 -
     A                                      MNU050H)
     A          H                           HLPARA(15 29 15 29)
     A                                      HLPPNLGRP(SC0002MPPGMT -
     A                                      MNU050H)
     A                                      HLPBDY
     A          H                           HLPARA(1 1 24 80)
     A                                      HLPPNLGRP(SC0002 -
     A                                      MNU050H)
     A            USER          10   O  1  2
     A                                      OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A                                      COLOR(BLU)
     A            WKCOMP        30   O  1 26
     A                                      OVRDTA
     A                                      OVRATR
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
     A                                  1 73DATE EDTCDE(Y)
     A                                      OVRATR
     A                                      DSPATR(HI)
     A                                      COLOR(BLU)
     A            PGM           10   O  2  2
     A                                      OVRATR
     A                                      DSPATR(HI)
     A                                      COLOR(BLU)
     A                                  2 26'   Work with Program Names    '
     A                                      OVRATR
     A                                      DSPATR(HI)
     A                                      COLOR(WHT)
     A                                  2 73TIME
     A                                      OVRATR
     A                                      DSPATR(HI)
     A                                      COLOR(BLU)
     A                                  4  2'Type choices, press Enter.'
     A                                      OVRATR
     A                                      COLOR(BLU)
     A                                  6  2'Program Name  . . . . . .'
     A                                      OVRATR
     A                                      COLOR(GRN)
     A            XPPGMN    R     A  B  6 29REFFLD(MPPGMN MNUPGM)
     A                                      DLTEDT
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(GRN)
     A  06                                  DSPATR(PC)
     A  06                                  DSPATR(RI)
     A  04                                  DSPATR(UL)
     A  03                                  DSPATR(PR)
     A                                  7  2'Secondary Program . . . .'
     A                                      OVRATR
     A                                      COLOR(GRN)
     A            XPSPGM    R     A  B  7 29REFFLD(MPSPGM MNUPGM)
     A                                      DLTEDT
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(GRN)
     A  04                                  DSPATR(UL)
     A  03                                  DSPATR(PR)
     A                                  8  2'Program Description . . .'
     A                                      OVRATR
     A                                      COLOR(GRN)
     A            XPPGMD    R     A  B  8 29REFFLD(MPPGMD MNUPGM)
     A                                      DLTEDT
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(GRN)
     A                                      CHECK(LC)
     A  05                                  DSPATR(PC)
     A  05                                  DSPATR(RI)
     A  02                                  DSPATR(UL)
     A  01                                  DSPATR(PR)
     A                                 10  2'Parameter 1 . . . . . . .'
     A                                      OVRATR
     A                                      COLOR(GRN)
     A            XPPRM1    R     A  B 10 29REFFLD(MPPRM1 MNUPGM)
     A                                      DLTEDT
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(GRN)
     A                                      CHECK(LC)
     A  02                                  DSPATR(UL)
     A  01                                  DSPATR(PR)
     A                                 11  2'Parameter 2 . . . . . . .'
     A                                      OVRATR
     A                                      COLOR(GRN)
     A            XPPRM2    R     A  B 11 29REFFLD(MPPRM2 MNUPGM)
     A                                      DLTEDT
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(GRN)
     A                                      CHECK(LC)
     A  02                                  DSPATR(UL)
     A  01                                  DSPATR(PR)
     A                                 12  2'Parameter 3 . . . . . . .'
     A                                      OVRATR
     A                                      COLOR(GRN)
     A            XPPRM3    R     A  B 12 29REFFLD(MPPRM3 MNUPGM)
     A                                      DLTEDT
     A                                      OVRDTA
     A                                      OVRATR
     A                                      COLOR(GRN)
     A                                      CHECK(LC)
     A  02                                  DSPATR(UL)
     A  01                                  DSPATR(PR)
     A                                 13  2'Parameter 4 . . . . . . .'
     A                                      OVRATR
     A                                      COLOR(GRN)
     A            XPPRM4    R     A  B 13 29REFFLD(MPPRM4 MNUPGM)
     A                                      DLTEDT
     A                                      COLOR(GRN)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      CHECK(LC)
     A  02                                  DSPATR(UL)
     A  01                                  DSPATR(PR)
     A                                 15  2'Program Type  . . . . . .'
     A                                      COLOR(GRN)
     A                                      OVRATR
     A            XPPGMT    R     A  B 15 29REFFLD(MPPGMT MNUPGM)
     A                                      DLTEDT
     A                                      COLOR(GRN)
     A                                      OVRDTA
     A                                      OVRATR
     A  07                                  DSPATR(PC)
     A  07                                  DSPATR(RI)
     A  02                                  DSPATR(UL)
     A  01                                  DSPATR(PR)
     A                                 23  2'F1=Help  F12=Cancel  F14=Delete'
     A                                      COLOR(BLU)
     A                                      OVRATR
     A*********************************************************************
     A          R PSSR0001                  CLRL(*NO) OVERLAY
     A                                  1 13'                                  -
     A                                                             '
     A                                      DSPATR(RI) COLOR(TRQ)
     A                                  2 13' ' DSPATR(RI) COLOR(TRQ)
     A                                  2 15'PSSR0001'
     A                                  2 24'       Execution Error Handler    -
     A                                                ' DSPATR(HI)
     A                                  2 69' ' DSPATR(RI) COLOR(TRQ)
     A                                  3 13' ' DSPATR(RI) COLOR(TRQ)
     A                                  3 15'                                  -
     A                                                         '
     A                                  3 69' ' DSPATR(RI) COLOR(TRQ)
     A                                  4 13' ' DSPATR(RI) COLOR(TRQ)
     A                                  4 15'Job . . . . :'
     A            WSID          10A  O  4 29COLOR(WHT)
     A                                  4 40'  Program  . . :'
     A            PGM           10A  O  4 57COLOR(WHT)
     A                                  4 69' ' DSPATR(RI) COLOR(TRQ)
     A                                  5 13' ' DSPATR(RI) COLOR(TRQ)
     A                                  5 15'User  . . . :'
     A            USER          10A  O  5 29COLOR(WHT)
     A                                  5 40'  Library  . . :'
     A            ULIB          10A  O  5 57COLOR(WHT)
     A                                  5 69' ' DSPATR(RI) COLOR(TRQ)
     A                                  6 13' ' DSPATR(RI) COLOR(TRQ)
     A                                  6 15'Number  . . :'
     A            UJOBN          6S 0O  6 29COLOR(WHT)
     A                                  6 36'      Compiled . . :'
     A            UCDAT          6Y 0O  6 57EDTCDE(Y)  COLOR(WHT)
     A                                  6 66'  '
     A                                  6 69' ' DSPATR(RI) COLOR(TRQ)
     A                                  7 13' ' DSPATR(RI) COLOR(TRQ)
     A                                  7 15'Routine . . :'
     A            UROUTN         8A  O  7 29COLOR(WHT)
     A                                  7 38'                  '
     A            UCTIM          6Y 0O  7 57EDTWRD('  :  :  ')  COLOR(WHT)
     A                                  7 66'  '
     A                                  7 69' ' DSPATR(RI) COLOR(TRQ)
     A                                  8 13' ' DSPATR(RI) COLOR(TRQ)
     A                                  8 15'Status Code :'
     A            USTAT4         4A  O  8 29COLOR(WHT)
     A                                  8 34'        Source Line. :'
     A            USRCLN         8A  O  8 57COLOR(WHT)
     A                                  8 66'  '
     A                                  8 69' ' DSPATR(RI) COLOR(TRQ)
     A                                  9 13' ' DSPATR(RI) COLOR(TRQ)
     A                                  9 15'Message ID  :'
     A            MSGID          7A  O  9 29COLOR(WHT)
     A                                  9 37'     File Name  . :'
     A            UFILE          8A  O  9 57COLOR(WHT)
     A                                  9 66'  '
     A                                  9 69' ' DSPATR(RI) COLOR(TRQ)
     A                                 10 13' ' DSPATR(RI) COLOR(TRQ)
     A                                 10 16'                                  -
     A                                                       '
     A                                 10 69' ' DSPATR(RI) COLOR(TRQ)
     A                                 11 13' ' DSPATR(RI) COLOR(TRQ)
     A            UMESG1        51A  O 11 15DSPATR(HI) COLOR(RED)
     A                                 11 67' '
     A                                 11 69' ' DSPATR(RI) COLOR(TRQ)
     A                                 12 13' ' DSPATR(RI) COLOR(TRQ)
     A            UMESG2        51A  O 12 15DSPATR(HI) COLOR(RED)
     A                                 12 69' ' DSPATR(RI) COLOR(TRQ)
     A                                 13 13' ' DSPATR(RI) COLOR(TRQ)
     A            UMESG3        30A  O 13 15DSPATR(HI) COLOR(RED)
     A                                 13 46'                      '
     A                                 13 69' ' DSPATR(RI) COLOR(TRQ)
     A                                 14 13' ' DSPATR(RI) COLOR(TRQ)
     A                                 14 15'                                  -
     A                                                         '
     A                                 14 69' ' DSPATR(RI) COLOR(TRQ)
     A                                 15 13' ' DSPATR(RI) COLOR(TRQ)
     A                                 15 15'Select one of the following option-
     A                                      s. Then Enter.     '
     A                                 15 69' ' DSPATR(RI) COLOR(TRQ)
     A                                 16 13' ' DSPATR(RI) COLOR(TRQ)
     A                                 16 15'                                  -
     A                                                         '
     A                                 16 69' ' DSPATR(RI) COLOR(TRQ)
     A                                 17 13' ' DSPATR(RI) COLOR(TRQ)
     A            UEROPT         1A  B 17 15DSPATR(UL) DSPATR(HI)
     A                                      VALUES('1' '2' '3')
     A                                 17 17' 1. Continue at the beginning of p-
     A                                      rogram           '
     A                                 17 69' ' DSPATR(RI) COLOR(TRQ)
     A                                 18 13' ' DSPATR(RI) COLOR(TRQ)
     A                                 18 15'   2. Cancel the processing of the-
     A                                       program           '
     A                                 18 69' ' DSPATR(RI) COLOR(TRQ)
     A                                 19 13' ' DSPATR(RI) COLOR(TRQ)
     A                                 19 15'   3. Return control to the system-
     A                                       error handler     '
     A                                 19 69' ' DSPATR(RI) COLOR(TRQ)
     A                                 20 13' ' DSPATR(RI) COLOR(TRQ)
     A                                 20 15'                                  -
     A                                                         '
     A                                 20 69' ' DSPATR(RI) COLOR(TRQ)
     A                                 21 13' ' DSPATR(RI) COLOR(TRQ)
     A                                 21 15'Enter                             -
     A                                                         '  DSPATR(HI)
     A                                 21 69' ' DSPATR(RI) COLOR(TRQ)
     A                                 22 13'                                  -
     A                                                             '
     A                                      DSPATR(RI) COLOR(TRQ)
     A*********************************************************************
     A          R MSGRCD                    TEXT('MSG SFL RECORD')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGM                       SFLPGMQ
     A*********************************************************************
     A          R MSGCTL                    TEXT('MSG SFL CONTROL')
     A                                      OVERLAY SFLCTL(MSGRCD) SFLSIZ(10)
     A                                      SFLPAG(1) SFLDSPCTL SFLDSP SFLINZ
     A N98                                  SFLEND
     A            PGM                       SFLPGMQ
     A*
