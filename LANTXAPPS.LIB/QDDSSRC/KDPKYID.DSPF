     A******************************************************************
     A* Copyright    ** KDP Software Ltd. **      2002
     A******************************************************************
     A*
     A*  DISPLAY FILE ID     -    KDPKYI
     A*  DISPLAY FILE NAME   -    Input Security Key
     A*  DATE CREATED        -    Apr 4, 2002
     A*
     A******************************************************************
     A                                      PRINT
     A                                      CHGINPDFT
     A                                      INDARA
     A          R SCR001
     A N79                                  PUTOVR
     A                                      SETOFF(79)
     A  99                                  OVERLAY
     A                                      CF03 INDTXT(03 'Exit')
     A                                      CF12 INDTXT(12 'Return')
     A                                  1 30'KDP Software Ltd.'
     A                                      OVRATR
     A                                      DSPATR(HI)
     A                                  2 26'LandMark Security Key Entry'
     A                                      OVRATR
     A                                      DSPATR(HI)
     A                                  9 21'Enter your Security Number below:'
     A                                      OVRATR
     A            SECNUM        25   B 11 25
     A                                      DSPATR(UL)
     A                                      COLOR(GRN)
     A                                      OVRDTA
     A                                      OVRATR
     A                                 23  2'F3=Exit  F12=Return'
     A                                      COLOR(BLU)
     A                                      OVRATR
     A          R SCR002
     A N79                                  PUTOVR
     A                                      SETOFF(79)
     A                                      PROTECT
     A                                      OVERLAY
     A                                      CLRL(*NO)
     A                                 14 14'                                  -
     A                                                    '
     A                                      OVRATR
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                 15 14' '
     A                                      OVRATR
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                 15 16'                                  -
     A                                                '
     A                                 15 61' '
     A                                      OVRATR
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                 16 14' '
     A                                      OVRATR
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                 16 16'                                  -
     A                                                '
     A                                 16 61' '
     A                                      OVRATR
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                 17 14' '
     A                                      OVRATR
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                 17 17'LandMark may now be used until'
     A                                      OVRATR
     A                                      COLOR(GRN)
     A            WSDATE         8  0O 17 48
     A                                      DSPATR(HI)
     A                                      OVRDTA
     A                                      OVRATR
     A                                      EDTWRD('0 /  /    ')
     A                                 17 57'   '
     A                                 17 61' '
     A                                      DSPATR(RI)
     A                                      OVRATR
     A                                      COLOR(BLU)
     A                                 18 14' '
     A                                      DSPATR(RI)
     A                                      OVRATR
     A                                      COLOR(BLU)
     A                                 18 16'                                  -
     A                                                '
     A                                 18 61' '
     A                                      DSPATR(RI)
     A                                      OVRATR
     A                                      COLOR(BLU)
     A                                 19 14' '
     A                                      DSPATR(RI)
     A                                      OVRATR
     A                                      COLOR(BLU)
     A                                 19 16'      '
     A                                 19 23'Press enter to continue'
     A                                      COLOR(BLU)
     A                                      OVRATR
     A                                 19 47'             '
     A                                 19 61' '
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                      OVRATR
     A                                 20 14'                                  -
     A                                                    '
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                      OVRATR
     A*********************************************************************
     A          R PSSR0001                  CLRL(*NO) OVERLAY
     A                                  1 13'                                  -
     A                                                             '
     A                                      DSPATR(RI) COLOR(TRQ)
     A                                  2 13' ' DSPATR(RI) COLOR(TRQ)
     A                                  2 15'PSSR0001'
     A                                  2 24'       Execution Error Handler    -
     A                                                ' DSPATR(HI)
     A                                  2 69' ' DSPATR(RI) COLOR(TRQ)
     A                                  3 13' ' DSPATR(RI) COLOR(TRQ)
     A                                  3 15'                                  -
     A                                                         '
     A                                  3 69' ' DSPATR(RI) COLOR(TRQ)
     A                                  4 13' ' DSPATR(RI) COLOR(TRQ)
     A                                  4 15'Job . . . . :'
     A            WSID          10A  O  4 29COLOR(WHT)
     A                                  4 40'  Program  . . :'
     A            PGM           10A  O  4 57COLOR(WHT)
     A                                  4 69' ' DSPATR(RI) COLOR(TRQ)
     A                                  5 13' ' DSPATR(RI) COLOR(TRQ)
     A                                  5 15'User  . . . :'
     A            USER          10A  O  5 29COLOR(WHT)
     A                                  5 40'  Library  . . :'
     A            ULIB          10A  O  5 57COLOR(WHT)
     A                                  5 69' ' DSPATR(RI) COLOR(TRQ)
     A                                  6 13' ' DSPATR(RI) COLOR(TRQ)
     A                                  6 15'Number  . . :'
     A            UJOBN          6S 0O  6 29COLOR(WHT)
     A                                  6 36'      Compiled . . :'
     A            UCDAT          6Y 0O  6 57EDTCDE(Y)  COLOR(WHT)
     A                                  6 66'  '
     A                                  6 69' ' DSPATR(RI) COLOR(TRQ)
     A                                  7 13' ' DSPATR(RI) COLOR(TRQ)
     A                                  7 15'Routine . . :'
     A            UROUTN         8A  O  7 29COLOR(WHT)
     A                                  7 38'                  '
     A            UCTIM          6Y 0O  7 57EDTWRD('  :  :  ')  COLOR(WHT)
     A                                  7 66'  '
     A                                  7 69' ' DSPATR(RI) COLOR(TRQ)
     A                                  8 13' ' DSPATR(RI) COLOR(TRQ)
     A                                  8 15'Status Code :'
     A            USTAT4         4A  O  8 29COLOR(WHT)
     A                                  8 34'        Source Line. :'
     A            USRCLN         8A  O  8 57COLOR(WHT)
     A                                  8 66'  '
     A                                  8 69' ' DSPATR(RI) COLOR(TRQ)
     A                                  9 13' ' DSPATR(RI) COLOR(TRQ)
     A                                  9 15'Message ID  :'
     A            MSGID          7A  O  9 29COLOR(WHT)
     A                                  9 37'     File Name  . :'
     A            UFILE          8A  O  9 57COLOR(WHT)
     A                                  9 66'  '
     A                                  9 69' ' DSPATR(RI) COLOR(TRQ)
     A                                 10 13' ' DSPATR(RI) COLOR(TRQ)
     A                                 10 16'                                  -
     A                                                       '
     A                                 10 69' ' DSPATR(RI) COLOR(TRQ)
     A                                 11 13' ' DSPATR(RI) COLOR(TRQ)
     A            UMESG1        51A  O 11 15DSPATR(HI) COLOR(RED)
     A                                 11 67' '
     A                                 11 69' ' DSPATR(RI) COLOR(TRQ)
     A                                 12 13' ' DSPATR(RI) COLOR(TRQ)
     A            UMESG2        51A  O 12 15DSPATR(HI) COLOR(RED)
     A                                 12 69' ' DSPATR(RI) COLOR(TRQ)
     A                                 13 13' ' DSPATR(RI) COLOR(TRQ)
     A            UMESG3        30A  O 13 15DSPATR(HI) COLOR(RED)
     A                                 13 46'                      '
     A                                 13 69' ' DSPATR(RI) COLOR(TRQ)
     A                                 14 13' ' DSPATR(RI) COLOR(TRQ)
     A                                 14 15'                                  -
     A                                                         '
     A                                 14 69' ' DSPATR(RI) COLOR(TRQ)
     A                                 15 13' ' DSPATR(RI) COLOR(TRQ)
     A                                 15 15'Select one of the following option-
     A                                      s. Then Enter.     '
     A                                 15 69' ' DSPATR(RI) COLOR(TRQ)
     A                                 16 13' ' DSPATR(RI) COLOR(TRQ)
     A                                 16 15'                                  -
     A                                                         '
     A                                 16 69' ' DSPATR(RI) COLOR(TRQ)
     A                                 17 13' ' DSPATR(RI) COLOR(TRQ)
     A            UEROPT         1A  B 17 15DSPATR(UL) DSPATR(HI)
     A                                      VALUES('1' '2' '3')
     A                                 17 17' 1. Continue at the beginning of p-
     A                                      rogram           '
     A                                 17 69' ' DSPATR(RI) COLOR(TRQ)
     A                                 18 13' ' DSPATR(RI) COLOR(TRQ)
     A                                 18 15'   2. Cancel the processing of the-
     A                                       program           '
     A                                 18 69' ' DSPATR(RI) COLOR(TRQ)
     A                                 19 13' ' DSPATR(RI) COLOR(TRQ)
     A                                 19 15'   3. Return control to the system-
     A                                       error handler     '
     A                                 19 69' ' DSPATR(RI) COLOR(TRQ)
     A                                 20 13' ' DSPATR(RI) COLOR(TRQ)
     A                                 20 15'                                  -
     A                                                         '
     A                                 20 69' ' DSPATR(RI) COLOR(TRQ)
     A                                 21 13' ' DSPATR(RI) COLOR(TRQ)
     A                                 21 15'Enter                             -
     A                                                         '  DSPATR(HI)
     A                                 21 69' ' DSPATR(RI) COLOR(TRQ)
     A                                 22 13'                                  -
     A                                                             '
     A                                      DSPATR(RI) COLOR(TRQ)
     A*********************************************************************
     A          R MSGRCD                    TEXT('MSG SFL RECORD')
     A                                      SFL
     A                                      SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGM                       SFLPGMQ
     A*********************************************************************
     A          R MSGCTL                    TEXT('MSG SFL CONTROL')
     A                                      OVERLAY SFLCTL(MSGRCD) SFLSIZ(10)
     A                                      SFLPAG(1) SFLDSPCTL SFLDSP SFLINZ
     A N98                                  SFLEND
     A            PGM                       SFLPGMQ
     A*
