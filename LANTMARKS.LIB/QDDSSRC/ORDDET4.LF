     A**************************************************************************
     A* Copyright 2004    ** KDP Software Ltd **      All Rights Reserved      *
     A**************************************************************************
     A*                                                                        *
     A*  File Name:         ORDDET4                                            *
     A*  File Description:  Backorders in Whse/Item Seq.                       *
     A*                                                                        *
     A*  Modifications:                                                        *
     A*  Date---*  User-----*  Description-----------------------------------* *
     A*                                                                        *
     A**************************************************************************
     A*
     A          R ORDDETF                   PFILE(ORDDETSQ  )
     A            ODCOYNO        3A         TEXT('Company number')
     A                                      COLHDG('Cpy.' )
     A            ODSONO         7A         TEXT('S.O.Number')
     A                                      COLHDG('Order|Number' )
     A            ODSEQ          5P 0       TEXT('Sequence')
     A                                      COLHDG('Seq' )
     A                                      EDTCDE(Z)
     A            ODAUD          9P 0       TEXT('Audit number')
     A                                      COLHDG('Audit number' )
     A                                      EDTCDE(Z)
     A            ODITEM        20A         TEXT('Item Number')
     A                                      COLHDG('Item Number' )
     A            ODIDESC       40A         TEXT('Item Description')
     A                                      COLHDG('Item Description' )
     A            ODWHSE         3A         TEXT('Warehouse Number')
     A                                      COLHDG('Whse|No.' )
     A            ODLOCN        10A         TEXT('Location')
     A                                      COLHDG('Location' )
     A            ODQORD         9P 0       TEXT('Qty Ordered')
     A                                      COLHDG('Counted Qty' )
     A                                      EDTCDE(Z)
     A            ODQTSH         9P 0       TEXT('Qty To Ship')
     A                                      COLHDG('Counted Qty' )
     A                                      EDTCDE(Z)
     A            ODQOBO         9P 0       TEXT('Qty Backordered')
     A                                      COLHDG('Counted Qty' )
     A                                      EDTCDE(Z)
     A            ODQSHP         9P 0       TEXT('Qty Shipped')
     A                                      COLHDG('Counted Qty' )
     A                                      EDTCDE(Z)
     A            ODUOM          5A         TEXT('Unit of Measure')
     A                                      COLHDG('UOM' )
     A            ODUCST        12P 5       TEXT('Unit Cost')
     A                                      COLHDG('Purchase' +
     A                                             'Price' )
     A                                      EDTCDE(3)
     A            ODUPRC        11P 4       TEXT('List Price')
     A                                      COLHDG('List Price' )
     A                                      EDTCDE(3)
     A            ODVATC         2A         TEXT('VAT Code')
     A                                      COLHDG('VAT' )
     A            ODCUSCON      10A         TEXT('Contract number')
     A                                      COLHDG('Cont No.' )
     A            ODDISC         5P 2       TEXT('Discount %')
     A                                      COLHDG('Disc %' )
     A                                      EDTCDE(3)
     A            ODNLAS        15A         TEXT('N/L Sales Account')
     A                                      COLHDG('Sales Account' )
     A            ODNLST        15A         TEXT('N/L Stock Account')
     A                                      COLHDG('Stock Account' )
     A            ODNLAC        15A         TEXT('N/L Cost of Sales Account')
     A                                      COLHDG('Cost of Sales' )
     A            ODNETV        13P 2       TEXT('Net value')
     A                                      COLHDG('VALUE' )
     A                                      EDTCDE(3)
     A            ODCSTV        13P 2       TEXT('Cost Value')
     A                                      COLHDG('VALUE' )
     A                                      EDTCDE(3)
     A            ODVATV        13P 2       TEXT('Vat Value')
     A                                      COLHDG('VALUE' )
     A                                      EDTCDE(3)
     A            ODENTS         1A         TEXT('Entry Status')
     A                                      COLHDG('Entry|Status' )
     A            ODPCKS         1A         TEXT('Picked Status')
     A                                      COLHDG('Picked|Status' )
     A            ODINVS         1A         TEXT('Invoice Status')
     A                                      COLHDG('Invoice|Status' )
     A            ODACKF         1A         TEXT('Acknowledgment printed')
     A                                      COLHDG('Ack|Printed' )
     A            ODMISC         1A         TEXT('Miscellaneous Item')
     A                                      COLHDG('Yes/No' )
     A            ODLDSD         8S 0       TEXT('Last despatch date')
     A                                      COLHDG('Date' )
     A                                      EDTWRD('    /  /  ')
     A            ODDSPN         7P 0       TEXT('Despatch Note Number')
     A                                      COLHDG('Despatch|Note' )
     A                                      EDTCDE(Z)
     A            ODLIND         8S 0       TEXT('Last Invoice Date')
     A                                      COLHDG('Date' )
     A                                      EDTWRD('    /  /  ')
     A            ODINVN         7P 0       TEXT('Invoice Number')
     A            ODJOBN        10A         TEXT('Job Number')
     A                                      COLHDG('Job|Number' )
     A            ODUSD1        10A         TEXT('User Field 1')
     A                                      COLHDG('User Field' )
     A            ODUSD2        10A         TEXT('User Field 2')
     A                                      COLHDG('User Field' )
     A            ODUSD3        10A         TEXT('User Field 3')
     A                                      COLHDG('User Field' )
     A            ODUSD4        10A         TEXT('User Field 4')
     A                                      COLHDG('User Field' )
     A            ODUSD5        10A         TEXT('User Field 5')
     A                                      COLHDG('User Field' )
     A          K ODCOYNO
     A          K ODWHSE
     A          K ODITEM
     A          K ODSONO
     A          K ODSEQ
     A*
     A          S ODQOBO                    COMP(GT 0)
     A*
